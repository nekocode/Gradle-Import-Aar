import com.android.build.gradle.internal.dependency.ExtractAarTransform
import com.android.build.gradle.internal.dependency.AarTransform
import com.android.build.gradle.internal.publishing.AndroidArtifacts.ArtifactType


apply plugin: 'java-library'

def artifactType = Attribute.of('artifactType', String)

dependencies {
    /*
      Android Jar
     */
    def targetSdkVersion = 26
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def sdkDir = properties.getProperty('sdk.dir')

    compileOnly files("${sdkDir}/platforms/android-${targetSdkVersion}/android.jar")


    /*
      Aars
     */
    registerTransform {
        from.attribute(artifactType, "aar")
        to.attribute(artifactType, "aarExtracted")
        artifactTransform(ExtractAarTransform)
    }

    registerTransform {
        from.attribute(artifactType, "aarExtracted")
        to.attribute(artifactType, "jar")
        artifactTransform(AarTransform) { params(ArtifactType.JAR) }
    }

    configurations {
        aar {
            attributes {
                attribute(artifactType, "jar")
            }
        }
    }

    def SUPPORT_LIBS_VERSION = '27.0.2'
    aar "com.android.support:appcompat-v7:$SUPPORT_LIBS_VERSION"
    aar "com.android.support:recyclerview-v7:$SUPPORT_LIBS_VERSION"

    configurations.aar.files.collect {
        /*
          Import the jars extracted from the aar packages
         */
        compileOnly files(it.absolutePath)
    }
}